import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'maven-publish'
    id 'p3d.java-conventions'
    id 'org.springframework.boot' version "$spring_boot_version" apply false
    id 'io.spring.dependency-management' version "$spring_dependencies_plugin_version"
    id "io.freefair.lombok" version "$lombok_plugin_version"

}

dependencies {
    implementation project(':core')
    testImplementation "org.junit.jupiter:junit-jupiter"
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

publishing {
    repositories {
        // Das Ziel-Repository
        // The target repository
        maven {
            // Der Name kann beliebig gewählt werden
            // Choose whatever name you want
            name = "GitHubPackages"
            // Die URL des Repositories, in dem die Artefakte veröffentlicht werden sollen
            // The url of the repository, where the artifacts will be published
            url = "https://maven.pkg.github.com/qw3rtrun/p3d-web-ui"
            credentials {
                // Die Zugangsdaten (weiter unten beschrieben)
                // The credentials (described in the next section)
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            // Behebt den Fehler mit den dynamischen Versionen von Spring Boot
            // Fixes the error with dynamic versions when using Spring Boot
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
